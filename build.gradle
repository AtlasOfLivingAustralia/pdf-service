buildscript {
    ext {
        kotlinVersion = '0.12.1230'
    }
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    ext {
        dropwizardVersion = '0.8.2'
    }
    group = 'au.org.ala'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = 1.7

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
  
}

project(':pdfgen-application') {
    dependencies {
        compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
        compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
        compile "io.dropwizard:dropwizard-forms:$dropwizardVersion"
    }
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'docker'

mainClassName = 'au.org.ala.PdfGen'

def devconfig = 'devconfig.yml'

run { args 'server', devconfig }

dependencies {
    compile project(':pdfgen-application')
}

shadowJar {
    exclude 'LICENSE'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    mergeServiceFiles()
}

docker {
    baseImage "phusion/baseimage:0.9.17"
    maintainer 'Simon Bear "simon.bear@csiro.au"'
}

task pdfgenDocker(type: Docker) {
    dependsOn 'shadowJar'
    applicationName = "pdfgen"
    defaultCommand = ['/sbin/my_init']
    exposePort 9090
    volume '/storage'
    runCommand 'apt-add-repository -y ppa:libreoffice/libreoffice-5-0'
    runCommand 'apt-get update && apt-get install -y openjdk-7-jre-headless && apt-get install -y libreoffice unoconv xfonts-mathml fonts-dejavu fonts-sil-gentium-basic fonts-sil-gentium fonts-liberation --no-install-recommends --no-install-suggests'
    runCommand 'mkdir /etc/service/pdfgen'
    runCommand 'mkdir /etc/service/soffice'
    runCommand 'mkdir -p /var/lib/pdfgen'
    runCommand 'mkdir -p /opt/pdfgen'
    runCommand 'mkdir -p /etc/pdfgen'
    addFile 'image/pdfgen.sh', '/etc/service/pdfgen/run'
    addFile 'image/soffice.sh', '/etc/service/soffice/run'
    addFile "build/libs/pdfgen-$version-all.jar", '/opt/pdfgen/pdfgen.jar'
    addFile 'image/config.yml', '/etc/pdfgen/config.yml'
    runCommand 'apt-get clean && rm -rf /var/lib/apt/lists/* /tmp/* /var/tmp/*'
}
