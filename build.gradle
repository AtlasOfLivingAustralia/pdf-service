buildscript {
    ext {
        kotlinVersion = '1.0.0'
    }
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    ext {
        dropwizardVersion = '0.9.2'
    }
    group = 'au.org.ala'
    version = '1.1-SNAPSHOT'
    sourceCompatibility = 1.7

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.6.5-2"
    }
  
}

project(':pdfgen-application') {
    dependencies {
        compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
        compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
        compile "io.dropwizard:dropwizard-forms:$dropwizardVersion"
    }
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'docker'

mainClassName = 'au.org.ala.PdfGen'

def devconfig = 'devconfig.yml'

run { args 'server', devconfig }

dependencies {
    compile project(':pdfgen-application')
}

shadowJar {
    exclude 'LICENSE'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    mergeServiceFiles()
}

docker {
    baseImage "phusion/baseimage:0.9.22"
    maintainer 'Simon Bear "simon.bear@csiro.au"'
}

task loDocker(type: Docker) {
    applicationName "libreoffice"
    tagVersion "5.0"
    runCommand 'add-apt-repository -y ppa:libreoffice/ppa'
    runCommand 'DEBIAN_FRONTEND=noninteractive apt-get update'
    runCommand 'DEBIAN_FRONTEND=noninteractive apt-get install -y libreoffice unoconv fonts-dejavu fonts-sil-gentium-basic fonts-sil-gentium fonts-liberation --no-install-recommends --no-install-suggests'
    runCommand 'apt-get clean && rm -rf /var/lib/apt/lists/* /tmp/* /var/tmp/*'
}

task pdfgenDocker(type: Docker) {
    dependsOn 'shadowJar', 'loDocker'
    baseImage 'au.org.ala/libreoffice:5.0'
    applicationName "pdfgen"
    defaultCommand = ['/sbin/my_init']
    exposePort 9090
    runCommand 'DEBIAN_FRONTEND=noninteractive apt-get update'
    runCommand 'DEBIAN_FRONTEND=noninteractive apt-get install -y openjdk-8-jre-headless xz-utils'
    runCommand 'apt-get clean && rm -rf /var/lib/apt/lists/* /tmp/* /var/tmp/*'
    runCommand 'mkdir -p /opt/wkhtmltopdf-0.12.4'
    runCommand 'curl -L https://github.com/wkhtmltopdf/wkhtmltopdf/releases/download/0.12.4/wkhtmltox-0.12.4_linux-generic-amd64.tar.xz > ./wkhtmltox-0.12.4_linux-generic-amd64.tar.xz'
    runCommand 'tar xf wkhtmltox-0.12.4_linux-generic-amd64.tar.xz -C /opt/wkhtmltopdf-0.12.4'
    runCommand 'rm wkhtmltox-0.12.4_linux-generic-amd64.tar.xz'
    runCommand 'ln -s /opt/wkhtmltopdf-0.12.4/wkhtmltox/bin/wkhtmltopdf /usr/bin/wkhtmltopdf'
    runCommand 'useradd -r -s /bin/false -m pdfgen'
    runCommand 'mkdir -p /storage/pdfs'
    runCommand 'chown pdfgen /storage/pdfs'
    volume '/storage'
    runCommand 'mkdir /etc/service/pdfgen'
    addFile 'image/pdfgen.sh', '/etc/service/pdfgen/run'
    runCommand 'mkdir -p /opt/pdfgen'
    runCommand 'mkdir -p /etc/pdfgen'
    addFile 'image/config.yml', '/etc/pdfgen/config.yml'
    addFile "build/libs/pdfgen-$version-all.jar", '/opt/pdfgen/pdfgen.jar'
}
